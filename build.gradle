plugins {
	id 'java-library'
	id 'maven-publish'
}

apply plugin: 'java'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(7)
	}
}

repositories {
	mavenCentral()
	jcenter()
	maven {
		name = 'mojang'
		url = 'https://libraries.minecraft.net/'
	}
}

version = project.version
def versionText
task checkRelease {
	if (!(System.getenv("BUILD_RELEASE") == "true")) {
		version += ("-SNAPSHOT+" + getTimestampSuffix())
		versionText = project.devVersionText
	} else {
		versionText = project.versionText
	}
}

def getTimestampSuffix() {
	   def date = new Date()
	   return date.format('yyyy-MM-dd_HHmmss')
}

sourceSets {
   main {
	  java {
		 srcDir 'src/main/java'
	  }
   }
}

dependencies {
	// asm
	api 'org.ow2.asm:asm-all:5.2'

	// launchwrapper + dependencies
	api ('net.minecraft:launchwrapper:1.12') {
		transitive = false
	}
	api 'net.sf.jopt-simple:jopt-simple:5.0.3'
}

task processSource (type: Sync, dependsOn: 'checkRelease') {

	outputs.upToDateWhen { false }

	from(sourceSets.main.java) {
		// only process this file
		include 'com/zero/retrowrapper/util/Constants.java'

		// replace build variables
		expand 'version':version, 'versionText':versionText
	}

	from(sourceSets.main.java) {
		exclude 'com/zero/retrowrapper/util/Constants.java'
	}

	into "$buildDir/processedSource"
}

compileJava {
	//use the processed source with replaced build variables
	source = processSource.outputs
}

jar {
	manifest {
		attributes (
			'Main-Class': 'com.zero.retrowrapper.installer.Installer'
		)
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
				artifact(file("${project.buildDir}/libs/$archivesBaseName-${version}.jar")) {
					builtBy build
			}
		}
	}
}
